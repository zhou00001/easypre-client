plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.easypre"
archivesBaseName = "easypre-client"
version = "1.0.2"
description = "This is EasyPre Client!"

repositories{
    mavenLocal()
    mavenCentral()
}
ext{
    guavaVersion = "29.0-jre"
    okhttpVersion = "4.6.0"
    commonsLang3Version = "3.10"
    commonsCodecVersion = "1.14"
    guavaRetryVersion = "2.0.0"
    slf4jVersion = "1.7.30"
    jacksonVersion = "2.11.0"
    junitVersion = "4.13"
}
dependencies {
    compile group: 'com.google.guava', name: 'guava', version: "$guavaVersion"
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: "$okhttpVersion"
    compile group: 'org.apache.commons', name: 'commons-lang3', version: "$commonsLang3Version"
    compile group: 'commons-codec', name: 'commons-codec', version: "$commonsCodecVersion"
    compile group: 'com.github.rholder', name: 'guava-retrying', version: "$guavaRetryVersion"
    compile group: 'org.slf4j', name: 'slf4j-api', version: "$slf4jVersion"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "$jacksonVersion"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "$jacksonVersion"

    testCompile group: 'junit', name: 'junit', version: "$junitVersion"
}

task sourcesJar(type: Jar){
    from sourceSets.main.allJava
    classifier = 'sources'
}
task javadocJar(type: Jar){
    from javadoc
    classifier = 'javadoc'
}
javadoc{
    options{
        encoding "UTF-8"
        charSet "UTF-8"
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "easypre-client"
                description = "this is easypre client."
                url = "https://github.com/zhou00001/easypre-client"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "zhou00001"
                        name = "zhou00001"
                        email = "zhou00001@126.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/zhou00001/easypre-client.git"
                    developerConnection = "scm:git:https://github.com/zhou00001/easypre-client.git"
                    url = "https://github.com/zhou00001/easypre-client"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            // 如果version以SNAPSHOT结尾就会上传到快照仓库，如果不是就上传到release仓库
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }

    }
}

signing {
    sign publishing.publications.mavenJava
}
